plugins {
	id 'java'
	id 'fabric-loom' version '1.2.7'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

if(System.getenv("BUILD_NUMBER") != null) {
	archivesBaseName = "${project.archives_base_name}-nightly"
	version = "${project.mod_version}+${System.getenv("BUILD_NUMBER")}"
} else {
	archivesBaseName = project.archives_base_name
	if(System.getenv("TAG_NAME") != null) {
		version = System.getenv("TAG_NAME")
	} else {
		version = project.mod_version
	}
}

group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
		url 'https://jitpack.io'
	}
	maven {
		name = "Ladysnake Mods"
		url = "https://maven.ladysnake.org/releases"
		content {
			includeGroup("io.github.ladysnake")
			includeGroupByRegex("dev\\.emi.*")
			includeGroupByRegex("dev\\.onyxstudios.*")
		}
	}
	maven {
		url = "https://maven.pkg.github.com/minefortress-mod/minefortress-cc-api"
		credentials {
			username = System.getenv("GITHUB_USERNAME")
			password = System.getenv("GITHUB_TOKEN")
		}
	}
	maven {
		url = "https://maven.pkg.github.com/minefortress-mod/automatone"
		credentials {
			username = System.getenv("GITHUB_USERNAME")
			password = System.getenv("GITHUB_TOKEN")
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation include("com.github.Chocohead:Fabric-ASM:v2.3")

	modImplementation include("io.github.ladysnake:automatone:1.0.3+minefortress")
	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.2.6+minefortress")
	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.6+minefortress")
	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-world:5.2.6+minefortress")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

loom {
	accessWidenerPath = file("src/main/resources/minefortress.accesswidener")
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/" + (System.getenv("GITHUB_REPOSITORY") ?: "minefortress-mod/minefortress"))
			credentials {
				username = System.getenv("GITHUB_USERNAME")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifactId = project.archivesBaseName
			from components.java
			pom {
				description = System.getenv("GITHUB_COMMITS_TEXT") ?: "No description provided"
			}
		}
	}
}